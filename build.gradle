ext.githubProjectName = 'RxJava'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            //FIXME: waiting for https://github.com/johnrengelman/shadow/pull/38 to merge
            name 'Shadow'
            url 'http://dl.bintray.com/content/gvsmirnov/gradle-plugins'
        }
        jcenter()
    }

    dependencies {
        // Required for benchmarks
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8.1'
    }

    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    repositories { 
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    group = "com.netflix.rxjava"

    // make 'examples' use the same classpath
    configurations { 
        examplesCompile.extendsFrom compile
        examplesRuntime.extendsFrom runtime
    }


    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
    }

    sourceSets {
        examples
        perf
    }

    tasks.build { 
        //include 'examples' in build task
        dependsOn(examplesClasses)
    }

    dependencies {
        perfCompile 'org.openjdk.jmh:jmh-core:0.5.3'
        perfCompile 'org.openjdk.jmh:jmh-generator-annprocess:0.5.3'

        perfCompile project
    }
    
    eclipse {  
        classpath {
            plusConfigurations += configurations.perfCompile
            
            downloadSources = true
            downloadJavadoc = true
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus  += configurations.perfCompile
            scopes.PROVIDED.minus += configurations.compile
        }
    }

    task perfJar(type: Jar, dependsOn: perfClasses) {
        from sourceSets.perf.output + sourceSets.main.output
    }

    task benchmarks(dependsOn: perfJar) {

        apply plugin: "shadow"

        shadow {
            classifier = "benchmarks"
            includeDependenciesFor = ["runtime", "perfRuntime"]

            transformer(com.github.jengelman.gradle.plugins.shadow.transformers.ManifestResourceTransformer) {
                mainClass = "org.openjdk.jmh.Main"
            }
        }

        doLast {
            shadowJar.execute()
        }

    }
}

project(':rxjava-core') {
    sourceSets.test.java.srcDir 'src/test/java'
}
